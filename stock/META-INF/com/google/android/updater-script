# Reference: https://source.android.com/devices/tech/ota/inside_packages.html#edify-syntax


# Create and show a progress bar that can go up to 100%
show_progress(1,0);


ui_print("Mounting /system");
# Next line taken from updater-script from Moto G 2015 Android 5.1.1 OTA update
mount("ext4", "EMMC", "/dev/block/bootdevice/by-name/system", "/system", "max_batch_time=0,commit=1,data=ordered,barrier=1,errors=panic,nodelalloc");


# Update progress to 25%
set_progress(0.25);


# Update boot animation
if
    # Test to see if we're running a stock ROM
    # We need to get this directly from /system.build.prop since recoveries can override values from getprop()
    file_getprop("/system/build.prop", "ro.modversion") == ""
then
    ui_print("Stock ROM detected");
    # Try to put bootanimation.zip in /data/local if possible since it's less intrusive
    # Unfortunately support for this was dropped from Motorola's /system/bin/bootanimation in 2017
    if
        run_program("/sbin/grep", "/data/local/moodle/bootanimation.zip", "/system/bin/bootanimation") == "0"
    then
        ui_print("Updating boot animation in /data/local/moodle/");
        run_program("/sbin/mkdir", "-p", "/data/local/moodle");
        package_extract_file("bootanimation.zip", "/data/local/moodle/bootanimation.zip");
        set_metadata_recursive("/data/local/moodle", "uid", 1003, "gid", 9004, "fmode", 0660, "dmode", 0770, "selabel", "u:object_r:moodle_data_file:s0");
    else
        ui_print("WARNING: Unsupported device. Boot animation not updated.");
        ui_print("ro.product.device=" + getprop("ro.product.device"));
        ui_print("ro.build.product=" + getprop("ro.build.product"));
        ui_print("ro.cm.device=" + getprop("ro.cm.device"));
    endif;
else
    ui_print("Custom ROM detected");
    abort("ERROR: This package is only for stock ROMs. Please use a package for custom ROMs. Aborting.");
endif;


# Update progress to 50%
set_progress(0.5);


# Update logo based on the device
ui_print("Updating boot logo");
if
    # Moto G 2013
    is_substring("falcon", getprop("ro.product.device")) || is_substring("falcon", getprop("ro.build.product")) || is_substring("falcon", getprop("ro.cm.device")) ||
    getprop("ro.product.device") == "xt1032" || getprop("ro.build.product") == "xt1032" || getprop("ro.cm.device") == "xt1032" ||
    is_substring("peregrine", getprop("ro.product.device")) || is_substring("peregrine", getprop("ro.build.product")) || is_substring("peregrine", getprop("ro.cm.device"))
then
    package_extract_file("logo/moto-g-2013/logo.bin", "/dev/block/platform/msm_sdcc.1/by-name/logo");
else
    if
        # Moto G 2014
        is_substring("titan", getprop("ro.product.device")) || is_substring("titan", getprop("ro.build.product")) || is_substring("titan", getprop("ro.cm.device")) ||
        is_substring("thea", getprop("ro.product.device")) || is_substring("thea", getprop("ro.build.product")) || is_substring("thea", getprop("ro.cm.device"))
    then
        package_extract_file("logo/moto-g-2014/logo.bin", "/dev/block/platform/msm_sdcc.1/by-name/logo");
    else
        if
            # Moto G 2015
            is_substring("osprey", getprop("ro.product.device")) || is_substring("osprey", getprop("ro.build.product")) || is_substring("osprey", getprop("ro.cm.device"))
        then
            package_extract_file("logo/moto-g-2015/logo.bin", "/dev/block/bootdevice/by-name/logo");
        else
            if
                # Moto E4
                is_substring("perry", getprop("ro.product.device")) || is_substring("woods", getprop("ro.product.device")) ||
                # Moto E4 Plus
                is_substring("nicklaus", getprop("ro.product.device")) || is_substring("owens", getprop("ro.product.device"))
            then
                package_extract_file("logo/moto-e4/logo.bin", "/dev/block/bootdevice/by-name/logo");
            else
                ui_print("WARNING: Unsupported device. Logo not updated.");
                ui_print("ro.product.device=" + getprop("ro.product.device"));
                ui_print("ro.build.product=" + getprop("ro.build.product"));
                ui_print("ro.cm.device=" + getprop("ro.cm.device"));
            endif;
        endif;
    endif;
endif;


# Update progress to 75%
set_progress(0.75);


ui_print("Unmounting /system");
ifelse(is_mounted("/system") == "/system", unmount("/system"));


# Update progress to 100%
set_progress(1);
ui_print("Done");
