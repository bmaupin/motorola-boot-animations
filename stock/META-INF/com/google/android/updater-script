# Reference: https://source.android.com/devices/tech/ota/inside_packages.html#edify-syntax


# Create and show a progress bar that can go up to 100%
show_progress(1,0);


# ro.product.device can be found in /system/build.prop, but for some reason TWRP also puts it in /default.prop with its own value
#    e.g. osprey instead of osprey_u2
ui_print("Make note of this in case of issues: ro.product.device=" + getprop("ro.product.device"));


# Update logo based on the device
ui_print("Updating boot logo");
if
    # Moto G 2013
    is_substring("falcon", getprop("ro.product.device")) ||
    # Moto G 2013 (twrp-2.8.6.0-xt1032.img)
    getprop("ro.product.device") == "xt1032" ||
    # Moto G 2013 LTE
    is_substring("peregrine", getprop("ro.product.device"))
then
    package_extract_file("logo/moto-g-2013/logo.bin", "/dev/block/platform/msm_sdcc.1/by-name/logo");
else
    if
        # Moto G 2014
        is_substring("titan", getprop("ro.product.device")) ||
        # Moto G 2014 LTE
        is_substring("thea", getprop("ro.product.device"))
    then
        package_extract_file("logo/moto-g-2014/logo.bin", "/dev/block/platform/msm_sdcc.1/by-name/logo");
    else
        if
            # Moto G 2015
            is_substring("osprey", getprop("ro.product.device"))
        then
            package_extract_file("logo/moto-g-2015/logo.bin", "/dev/block/bootdevice/by-name/logo");
        else
            abort("ERROR: device mismatch. Logo not updated. Aborting.");
        endif;
    endif;
endif;


# Update progress to 50%
set_progress(0.5);


# Update boot animation
ui_print("Updating boot animation");
run_program("/sbin/mkdir", "-p", "/data/local/moodle");
# Always put bootanimation in /data/local since /system/bin/bootanimation will check there first and it won't break OTA updates
package_extract_file("bootanimation.zip", "/data/local/moodle/bootanimation.zip");
set_metadata_recursive("/data/local/moodle", "uid", 1003, "gid", 9004, "fmode", 0660, "dmode", 0770, "selabel", "u:object_r:moodle_data_file:s0");


# Update progress to 100%
set_progress(1);
ui_print("Done");
